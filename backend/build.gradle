import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "org.springframework.boot" version "3.2.0"
    id "io.spring.dependency-management" version "1.1.4"
    id "org.jetbrains.kotlin.jvm" version "1.9.21"
    id "org.jetbrains.kotlin.plugin.spring" version "1.9.21"
    id "org.openapi.generator" version "7.1.0"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

group = "com.retypeme"
version = "0.0.1-SNAPSHOT"

java {
    sourceCompatibility = "21"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-websocket"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0"
    implementation("org.openapitools:openapi-generator:7.1.0") {
        exclude group: "org.slf4j", module: "slf4j-simple"
        exclude group: "ch.qos.logback", module: "logback-classic"
    }
    implementation "javax.xml.bind:jaxb-api:2.2.4"
    implementation "org.web3j:core:4.10.3"
    implementation "org.web3j:parity:4.10.3"
    implementation "org.web3j:crypto:4.10.3"
    implementation "org.web3j:abi:4.10.3"
    implementation "org.web3j:codegen:4.10.3"
    implementation "org.web3j:infura:4.2.0"
    runtimeOnly "io.micrometer:micrometer-registry-prometheus"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

tasks.withType(KotlinCompile) {
    kotlinOptions {
        freeCompilerArgs += "-Xjsr305=strict"
        jvmTarget = "21"
    }
}

sourceSets {
    main.kotlin.srcDirs += "src/kotlin"
    main.kotlin.srcDirs += "src/java"
    main.kotlin.srcDirs += "build/generated"
}

compileKotlin.dependsOn tasks.named("openApiGenerate")

openApiGenerate {
    generatorName = "kotlin-spring"
    inputSpec.set("$rootDir/backend/src/main/resources/api-spec.yml")
    outputDir.set("$buildDir/generated")
    apiPackage.set("com.retypeme.project.api")
    modelPackage.set("com.retypeme.project.model")
    generateApiTests.set(false)
    generateModelTests.set(true)
    configOptions.set([
            useSpringBoot3      : "true",
            useJakartaEe        : "true",
            interfaceOnly       : "true",
            serviceInterface    : "false",
            serializableModel   : "true",
            skipDefaultInterface: "true"
    ])
}


tasks.named("test") {
    useJUnitPlatform()
}